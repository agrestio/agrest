/**
 * NOTES: This grammar is havily based on the Apache Cayenne Expression grammar,
 *        see https://github.com/apache/cayenne/blob/master/cayenne-server/src/main/jjtree/org/apache/cayenne/exp/parser/ExpressionParser.jjt
 *
 * Code generation instructions:
 *      - install JavaCC (see instructions at https://javacc.github.io/javacc/), if needed
 *      - open directory that contains this file in any console
 *      - run `jjtree AgExpressionParser.jjt` to produce `.jj` the JavaCC parser source and parser tree nodes
 *      - open source directory with a parser
 *      - run `javacc AgExpressionParser.jj` to produce the remaining Java code
 *      - update generics definition in `io.agrest.exp.parser.AgExpressionParserVisitor`
 *        and `io.agrest.exp.parser.AgExpressionParserDefaultVisitor` classes
 */

options {
	// JavaCC options
	MULTI = true;
	NODE_DEFAULT_VOID = true;
	STATIC = false;
	DEBUG_PARSER = false;
	DEBUG_LOOKAHEAD = false;
	DEBUG_TOKEN_MANAGER = false;
	JAVA_UNICODE_ESCAPE = true;
	UNICODE_INPUT = true;
	CACHE_TOKENS = true;
	JAVA_TEMPLATE_TYPE = "modern";
	OUTPUT_DIRECTORY = "../../../../../java/io/agrest/exp/parser/";

	// JJTree options
	VISITOR = true;
    VISITOR_RETURN_TYPE = "T"; // Need to add generics manually to a Visitor interface and DefaultVisitor class
    VISITOR_DATA_TYPE = "T";
    VISITOR_METHOD_NAME_INCLUDES_TYPE_NAME = false;
    NODE_CLASS = "AgExpression";
    NODE_PREFIX = "Exp"; // the default one "AST" will clash with the Cayenne parser
}

PARSER_BEGIN(AgExpressionParser)

package io.agrest.exp.parser;

// note: Parser doesn't use this import, but it's shared with the TokenManager
import java.io.StringReader;import java.math.BigDecimal;
import java.math.BigInteger;

import io.agrest.AgException;
import io.agrest.exp.SimpleNode;

/**
  * Parser of AgRest Expressions.
  *
  * @since 5.0
  */
public class AgExpressionParser {

    public static SimpleNode parse(String expressionString) {
        try {
            AgExpressionParser parser = new AgExpressionParser(new StringReader(expressionString));
            return parser.expression();
        } catch (ParseException e) {
            throw AgException.badRequest(e, e.getMessage());
        }
        catch (TokenMgrError e) {
            throw AgException.badRequest(e, e.getMessage());
        }
    }
}

PARSER_END(AgExpressionParser)

SimpleNode expression() : {}
{
	orCondition() <EOF> 
    { 
        return (SimpleNode) jjtree.rootNode();
    }
}

void orCondition() : {}
{
	( andCondition() ( "or" andCondition() )* ) #Or(>1)
}

void andCondition() : {}
{
	( notCondition() ( "and" notCondition() )* ) #And(>1)
}

void notCondition() : {}
{
		( "not" | "!" ) simpleCondition() #Not(1)
	|
		simpleCondition()
}

void simpleCondition() : {
    // variable arity for the Like nodes
    int arity = 2;
}
{
	  <TRUE> #True
	|
	  <FALSE> #False
	|
	  conditionExpression()
	(
          ("="  | "==") scalarExpression() #Equal(2)
        |
          ("!=" | "<>")  scalarExpression() #NotEqual(2)
        |
          "<=" scalarExpression() #LessOrEqual(2)
        |
          "<" scalarExpression() #Less(2)
        |
          ">"  scalarExpression() #Greater(2)
        |
          ">=" scalarExpression() #GreaterOrEqual(2)
        |
          "like" scalarExpression() ("escape" stringLiteral() {arity = 3;})? #Like(arity)
        |
          "likeIgnoreCase"  scalarExpression() ("escape" stringLiteral() {arity = 3;})? #LikeIgnoreCase(arity)
        |
          "in" ( namedParameter() | "(" scalarCommaList() ")" ) #In(2)
        |
          "between"  scalarExpression() "and" scalarExpression() #Between(3)
        |
          simpleNotCondition()
	)?
}

void simpleNotCondition() : {
    // variable arity for the Like nodes
    int arity = 2;
}
{
	("not" | "!" )
	(
		"like" scalarExpression() ("escape" stringLiteral() {arity=3;})? #NotLike(arity)
	|
	    "likeIgnoreCase"  scalarExpression() ("escape" stringLiteral() {arity=3;})? #NotLikeIgnoreCase(arity)
	|
		"in" (  namedParameter() |  "(" scalarCommaList() ")" ) #NotIn(2)
	|
		"between"  scalarExpression() "and" scalarExpression() #NotBetween(3)
	)
}

void scalarCommaList() : {}
{
	( scalarConstExpression() ( ","  scalarConstExpression() )* ) #ScalarList
}

void conditionExpression() : {}
{
        // Path without any operator will go to numeric expression.
        // This done intentionally to remove lookahead that leads
        // to dramatically perfomance degradation (parser can become 3 times slower)
		numericExpression()
    |
        stringExpression()
	|
		<NULL> #Scalar(0)
    |
        dateTimeFunction()
}

void stringParameter() : {}
{
        pathExpression()
    |
        stringExpression()
}

void stringLiteral() :  {}
{
    <SINGLE_QUOTED_STRING> { jjtThis.jjtSetValue(token_source.literalValue); } #Scalar(0)
    |
    <DOUBLE_QUOTED_STRING> { jjtThis.jjtSetValue(token_source.literalValue); } #Scalar(0)
}

void stringExpression() : {}
{
    stringLiteral()
    |
    functionsReturningStrings()
}

void scalarExpression() : {} 
{
  	   conditionExpression()
	|
	  <TRUE> { jjtThis.jjtSetValue(true); }  #Scalar(0)
	| 
	  <FALSE> { jjtThis.jjtSetValue(false); }  #Scalar(0)
}

void scalarConstExpression() : {}
{
	  <SINGLE_QUOTED_STRING> { jjtThis.jjtSetValue(token_source.literalValue); } #Scalar(0)
	|
	  <DOUBLE_QUOTED_STRING> { jjtThis.jjtSetValue(token_source.literalValue); } #Scalar(0)
	| 
	  namedParameter()
	| 
	  intLiteral()
	| 
	  floatLiteral()
	|
	  <TRUE>  { jjtThis.jjtSetValue(true);  } #Scalar(0)
	| 
	  <FALSE> { jjtThis.jjtSetValue(false); } #Scalar(0)
}

void numericExpression() : {}
{
    bitwiseOr()
}

void bitwiseOr() : {}
{
    bitwiseXor()
    (
        "|" bitwiseXor() #BitwiseOr(2)
    )*
}

void bitwiseXor() : {}
{
    bitwiseAnd()
    (
        "^" bitwiseAnd() #BitwiseXor(2)
    )*
}

void bitwiseAnd() : {}
{
    bitwiseShift()
    (
        "&" bitwiseShift() #BitwiseAnd(2)
    )*
}

void bitwiseShift(): {}
{
    arithmeticExp()
    (
        "<<" arithmeticExp() #BitwiseLeftShift(2)
      | 
        ">>" arithmeticExp() #BitwiseRightShift(2)
    )*
}


void arithmeticExp() : {}
{
    multiplySubtractExp() 
    ( 
        "+" multiplySubtractExp() #Add(2)
    |
        "-" multiplySubtractExp() #Subtract(2)
    )*
}

void multiplySubtractExp() : {} 
{
    numericTermExt() 
    ( 
        "*" numericTermExt() #Multiply(2)
    |
        "/" numericTermExt() #Divide(2)
    )*
}

void numericTermExt() : {}
{
        numericTerm()
    |
        "~" numericTerm()  #BitwiseNot(1)
}

  
void numericTerm() : {} 
{
        ( "+" )?  numericPrimary ()
    | 
        "-"  numericTerm() #Negate(1)
}


void numericPrimary() : {} 
{
		// technically we shouldn't make a condition a child of scalar expression
		// but this will be handled in the code to avoid long lookaheads
		
		"(" orCondition() ")"
	|
	    intLiteral()
	| 
	    floatLiteral()
	|
        namedParameter()
    |
        functionsReturningNumerics()
	|
        pathExpression()
}

void intLiteral() #Scalar(0) : {}
{
    <INT_LITERAL> { jjtThis.jjtSetValue(token_source.literalValue); }
}

void floatLiteral() #Scalar(0) : {}
{
    <FLOAT_LITERAL>{ jjtThis.jjtSetValue(token_source.literalValue); }
}

void functionsReturningStrings() : { }
{
	concat() | substring() | trim() | lower() | upper()
}

void concat() #Concat : { }
{
	<CONCAT> "(" stringParameter() ( "," stringParameter() )* ")"
}

void substring() #Substring : { }
{
	<SUBSTRING> "(" stringParameter() "," numericExpression() ["," numericExpression()] ")"
}

void trim() #Trim : { }
{
	<TRIM> "(" stringParameter() ")"
}

void lower() #Lower : { }
{
	<LOWER> "(" stringParameter() ")"
}

void upper() #Upper : { }
{
	<UPPER> "(" stringParameter() ")"
}

void functionsReturningNumerics() : { }
{
	length() | locate() | abs() | sqrt() | mod() | dateTimeExtractingFunction()
}

void length() #Length : { }
{
	<LENGTH> "(" stringParameter() ")"
}

void locate() #Locate : { }
{
	<LOCATE> "(" stringParameter() "," stringParameter() ["," numericExpression()] ")"
}

void abs() #Abs : { }
{
	<ABS> "(" numericExpression() ")"
}

void sqrt() #Sqrt : { }
{
	<SQRT> "(" numericExpression() ")"
}

void mod() #Mod : { }
{
	<MOD> "(" numericExpression() "," numericExpression() ")"
}

void dateTimeFunction() : { }
{
    currentDate() | currentTime() | currentTimestamp()
}

void currentDate() #CurrentDate : {}
{
    <CURRENT_DATE> "(" ")"
}

void currentTime() #CurrentTime : {}
{
    <CURRENT_TIME> "(" ")"
}

void currentTimestamp() #CurrentTimestamp : {}
{
    <CURRENT_TIMESTAMP> "(" ")"
}

/* Date/time parts extracting function */
void dateTimeExtractingFunction() #Extract(1) : {
    Token t;
}
{
    (
        t = <YEAR>
        | t = <MONTH>
        | t = <WEEK>
        | t = <DAY_OF_YEAR>
        | t = <DAY>
        | t = <DAY_OF_MONTH>
        | t = <DAY_OF_WEEK>
        | t = <HOUR>
        | t = <MINUTE>
        | t = <SECOND>
    ) {
        jjtThis.jjtSetValue(t.image);
    }
    "(" pathExpression() ")"
}

TOKEN_MGR_DECLS:
{
      /** Holds the last value computed by a constant token. */
    Object literalValue;
    
      /** Holds the last string literal parsed. */
    private StringBuffer stringBuffer;

      /** Converts an escape sequence into a character value. */
    private char escapeChar()
    {
        int ofs = image.length() - 1;
        switch ( image.charAt(ofs) ) {
            case 'n':   return '\n';
            case 'r':   return '\r';
            case 't':   return '\t';
            case 'b':   return '\b';
            case 'f':   return '\f';
            case '\\':  return '\\';
            case '\'':  return '\'';
            case '\"':  return '\"';
        }

          // Otherwise, it's an octal number.  Find the backslash and convert.
        while ( image.charAt(--ofs) != '\\' )
          {}
        int value = 0;
        while ( ++ofs < image.length() )
            value = (value << 3) | (image.charAt(ofs) - '0');
        return (char) value;
    }

    private Object makeInt() throws ParseException
    {
        Object  result;
        String  s = image.toString();
        int     base = 10;

        if ( s.charAt(0) == '0' )
            base = (s.length() > 1 && (s.charAt(1) == 'x' || s.charAt(1) == 'X'))? 16 : 8;
        if ( base == 16 )
            s = s.substring(2); // Trim the 0x off the front
        try {
            switch ( s.charAt(s.length()-1) ) {
                case 'l': case 'L':
                    result = Long.valueOf( s.substring(0,s.length()-1), base );
                    break;

                case 'h': case 'H':
                    result = new BigInteger( s.substring(0,s.length()-1), base );
                    break;

                default:
                    result = Integer.valueOf( s, base );
                    break;
            }
        } catch (NumberFormatException e) {
            throw new ParseException(e);
        }
        return result;
    }

    private Object makeFloat() throws ParseException
    {
        String s = image.toString();
        try {
            switch ( s.charAt(s.length()-1) ) {
                case 'f': case 'F':
                    Float fValue = Float.valueOf( s );
                    if (fValue.isInfinite()) {
                       throw new ParseException("Too large float value: " + image);
                    }
                    return fValue;

                case 'b': case 'B':
                    return new BigDecimal( s.substring(0,s.length()-1) );

                case 'd': case 'D':
                default:
                    Double dValue = Double.valueOf( s );
                    if (dValue.isInfinite()) {
                       throw new ParseException("Too large double value: " + image);
                    }
                    return dValue;
            }
        } catch (NumberFormatException e) {
            throw new ParseException(e);
        }
    }
}

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

TOKEN : {
	<NULL: "null" | "NULL" >
    |   <TRUE: "true" | "TRUE" >
    |   <FALSE: "false" | "FALSE" >
}

TOKEN : /* functions returning strings */
{
	<CONCAT: "concat" >
	|	<SUBSTRING: "substring" >
	|	<TRIM: "trim" >
	|	<LOWER: "lower" >
	|	<UPPER: "upper" >
}

TOKEN : /* functions returning numerics */
{
	<LENGTH: "length" >
	|	<LOCATE: "locate" >
	|	<ABS: "abs" >
	|	<SQRT: "sqrt" >
	|	<MOD: "mod" >
}

TOKEN : /* functions returning datetime */
{
	<CURRENT_DATE: "currentDate" >
	|	<CURRENT_TIME: "currentTime" >
	|	<CURRENT_TIMESTAMP: "currentTimestamp" | "now" >
}

/*
    Data/time parts extracting functions' names.
    MUST be in sync with enum ASTExtract.DateTimePart.
*/
TOKEN :
{
    <YEAR: "year">
    | <MONTH: "month">
    | <WEEK: "week">
    | <DAY_OF_YEAR: "dayOfYear">
    | <DAY: "day">
    | <DAY_OF_MONTH: "dayOfMonth">
    | <DAY_OF_WEEK: "dayOfWeek">
    | <HOUR: "hour">
    | <MINUTE: "minute">
    | <SECOND: "second">
}

void namedParameter() :{
	Token t;
}
{
	"$" (
        t = <PROPERTY_PATH>
        | t = <INT_LITERAL>
    ) {
        jjtThis.jjtSetValue(t.image);
    } #NamedParameter(0)
}

void pathExpression() : {
   Token t;
}
{
   t = <PROPERTY_PATH> { jjtThis.jjtSetValue(t.image); } #Path(0)
}

TOKEN : {
	<PROPERTY_PATH: <IDENTIFIER> ( "." <IDENTIFIER>) *>
}

TOKEN :
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>|<DOLLAR_SIGN>)* (["#"]<LETTER> (<LETTER>|<DIGIT>|<DOLLAR_SIGN>)*)? (["+"])? >
|
  < #LETTER: ["_","a"-"z","A"-"Z"] >
|
  < #DIGIT: ["0"-"9"] >
|
  < #DOLLAR_SIGN: ["$"] >
}

/**
 * Quoted Strings, whose object value is stored in the token manager's
 * "literalValue" field. Both single and double qoutes are allowed 
 */
MORE:
{
    "'"  { stringBuffer = new StringBuffer(); }: WithinSingleQuoteLiteral
 |
    "\""  { stringBuffer = new StringBuffer(); }: WithinDoubleQuoteLiteral
}

<WithinSingleQuoteLiteral> MORE:
{
	< ESC: "\\" ( ["n","r","t","b","f","\\","'","`","\""]
                | (["0"-"3"])? ["0"-"7"] (["0"-"7"])?
                )
    >
        { stringBuffer.append( escapeChar() ); }
 |
    < (~["'","\\"]) >
        { stringBuffer.append( image.charAt(image.length()-1) ); }
}

<WithinSingleQuoteLiteral> TOKEN :
{
    <SINGLE_QUOTED_STRING: "'">
        { literalValue = stringBuffer.toString(); }
        : DEFAULT
}

<WithinDoubleQuoteLiteral> MORE :
{
    < STRING_ESC: <ESC> >
        { stringBuffer.append( escapeChar() ); }
 |
    < (~["\"","\\"]) >
        { stringBuffer.append( image.charAt(image.length()-1) ); }
}

<WithinDoubleQuoteLiteral> TOKEN:
{
    <DOUBLE_QUOTED_STRING: "\"">
        { literalValue = stringBuffer.toString(); }
        : DEFAULT
}

/**
 * Integer or real Numeric literal, whose object value is stored in the token manager's
 * "literalValue" field.
 */
TOKEN:
{
    < INT_LITERAL:
        ( "0" (["0"-"7"])* | ["1"-"9"] (["0"-"9"])* | "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ )
        (["l","L","h","H"])?
    >
        { literalValue = makeInt(); }
 |
    < FLOAT_LITERAL:
        ( <DEC_FLT> (<EXPONENT>)? (<FLT_SUFF>)?
        | <DEC_DIGITS> <EXPONENT> (<FLT_SUFF>)?
        | <DEC_DIGITS> <FLT_SUFF>
        )
    >
        { literalValue = makeFloat(); }

 |  < #DEC_FLT: (["0"-"9"])+ "." (["0"-"9"])* | "." (["0"-"9"])+ >
 |  < #DEC_DIGITS: (["0"-"9"])+ >
 |  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
 |  < #FLT_SUFF: ["d","D","f","F","b","B"] >
}
